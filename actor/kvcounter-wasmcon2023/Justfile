just := env_var_or_default("JUST", just_executable())
cargo := env_var_or_default("CARGO", "cargo")
wasm_tools := env_var_or_default("WASM_TOOLS", "wasm-tools")
wash := env_var_or_default("WASH", "wash")

expected_wasm_path := "target/wasm32-wasi/release/wasmcon2023_keyvalue.wasm"
wasm_preview2_output_path := "target/wasm32-wasi/release/wasmcon2023_keyvalue.preview2.wasm"

_default:
    {{just}} --list

# Lint
lint:
    {{cargo}} clippy --all-features --all-targets

# Format
fmt:
    {{cargo}} fmt

# Build the WASM components using wasmcloud tooling (wash)
#
# NOTE: right now only wash version 0.20.2 works with this demo,
# due to upstream (wasmtime, etc) changes that modified the
# adapter and other ecosystem projects.
build:
    #!/bin/env -S bash -euo pipefail
    if [ "wash 0.20.2" != "$({{wash}} --version)" ]; then \
      echo "";
      echo "ðŸ›‘  [error] Only wash version 0.20.2 is currently supported for this demo.";
      echo "";
      echo "You can install wash version 0.20.2 with the rust toolchain:";
      echo "";
      echo "cargo install --version=0.20.2";
      echo "";
      exit -1;
    fi
    echo "[info] update your PATH or set the WASH environment variable to change the version of wash used"
    {{wash}} build

# Build the project, continuously
build-watch:
    {{cargo}} watch --ignore=target -- {{just}} build

##############
# Operations #
##############

# NOTE: don't forget to set up WASMCLOUD_CLUSTER_SEED and WASMCLOUD_CLUSTER_ISSUERS
# as necessary when starting the ðŸ¦€ rust host!

expected_signed_wasm_path := absolute_path("build/wasmcon2023_keyvalue_s.wasm")

# Run the actor on a given host
run-actor host_id:
    @echo "[warn] make sure to set WASMCLOUD_CLUSTER_SEED and WASMCLOUD_CLUSTER_ISSUERS appropriately for new hosts"
    @echo "(see output of wash up for expected values of cluster seed & issuers)"
    @{{wash}} start actor file://{{expected_signed_wasm_path}} --host-id {{host_id}}

# Set up links to a given in-lattice actor_id
setup-links actor_id:
    # Setting up links for HTTP provider (VAG3QITQQ2ODAOWB5TTQSDJ53XK3SHBEIFNK4AYJ5RKAX2UNSCAPHA5M)
    @{{wash}} link put {{actor_id}} VAG3QITQQ2ODAOWB5TTQSDJ53XK3SHBEIFNK4AYJ5RKAX2UNSCAPHA5M wasmcloud:httpserver
    # Setting up links for kvredis provider (VAG3QITQQ2ODAOWB5TTQSDJ53XK3SHBEIFNK4AYJ5RKAX2UNSCAPHA5M)
    @{{wash}} link put {{actor_id}} VAZVC4RX54J2NVCMCW7BPCAHGGG5XZXDBXFUMDUXGESTMQEJLC3YVZWB wasmcloud:keyvalue
